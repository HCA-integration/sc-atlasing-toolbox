from pathlib import Path
import pandas as pd

from utils.ModuleConfig import ModuleConfig
from utils.environments import get_env


mcfg = ModuleConfig(
    module_name='collect',
    config=config,
    config_params=[
        'same_slots',
        'merge_slots',
        'sep',
        'obs_index_col',
        'dask',
        'backed'
    ],
    mandatory_wildcards=['dataset'],
    dtypes=dict(
        dask=bool,
        backed=bool
    ),
)
params = mcfg.get_paramspace()

# TODO: allow for custom grouping of input files

wildcard_constraints:
    dataset=r'[^/]+',

module common:
    snakefile: "../common/Snakefile"
    config: config

use rule * from common as common_ *


rule collect:
    input:
        unpack(lambda wildcards: mcfg.get_input_files_per_dataset(dataset=wildcards.dataset))
    output:
        zarr=directory(mcfg.out_dir / f'{params.wildcard_pattern}.zarr'),
    params:
        sep=lambda wildcards: mcfg.get_from_parameters(wildcards, 'sep', default='_'),
        same_slots=lambda wildcards: mcfg.get_from_parameters(wildcards, 'same_slots', default=[]),
        merge_slots=lambda wildcards: mcfg.get_from_parameters(wildcards, 'merge_slots', default=[]),
        obs_index_col=lambda wildcards: mcfg.get_from_parameters(wildcards, 'obs_index_col', default={}),
    conda:
        get_env(config, 'scanpy', env_dir='envs')
    resources:
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='cpu',resource_key='mem_mb',attempt=attempt),
    script:
        'collect.py'


rule all:
    input:
        mcfg.get_output_files(rules.collect.output)
    default_target: True
    localrule: True


rule dependency_graph:
    input:
        expand(
            rules.common_dependency_graph.input,
            images=mcfg.image_dir,
            target='all'
        )
    localrule: True
