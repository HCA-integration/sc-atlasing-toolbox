from pathlib import Path
import pandas as pd

from utils.ModuleConfig import ModuleConfig
from utils.environments import get_env


mcfg = ModuleConfig(
    module_name='merge',
    config=config,
    config_params=[
        'merge_strategy',
        'dask',
        'backed',
        'threads',
        'stride',
    ],
    mandatory_wildcards=['dataset'],
    dtypes=dict(
        dask=bool,
        backed=bool,
        threads=int,
    ),
)
params = mcfg.get_paramspace()


wildcard_constraints:
    dataset=r'[^/]+',

module common:
    snakefile: "../common/Snakefile"
    config: config

use rule * from common as common_ *


rule merge:
    input:
        unpack(lambda wildcards: mcfg.get_input_files_per_dataset(dataset=wildcards.dataset))
    output:
        zarr=directory(mcfg.out_dir / f'{params.wildcard_pattern}.zarr'),
    params:
        merge_strategy=lambda wildcards: mcfg.get_from_parameters(wildcards, 'merge_strategy', default='inner'),
        dask=lambda wildcards: mcfg.get_from_parameters(wildcards, 'dask', default=False),
        backed=lambda wildcards: mcfg.get_from_parameters(wildcards, 'backed', default=False),
        stride=lambda wildcards: mcfg.get_from_parameters(wildcards, 'stride', default=500_000),
    conda:
        get_env(config, 'scanpy', env_dir='envs')
    threads:
        lambda wildcards: max(1, mcfg.get_from_parameters(wildcards, 'threads', default=1))
    resources:
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='cpu',resource_key='mem_mb',attempt=attempt, factor=3),
    script:
        'merge.py'


rule all:
    input:
        mcfg.get_output_files(rules.merge.output)
    default_target: True
    localrule: True


rule dependency_graph:
    input:
        expand(
            rules.common_dependency_graph.input,
            images=mcfg.image_dir,
            target='all'
        )
    localrule: True
