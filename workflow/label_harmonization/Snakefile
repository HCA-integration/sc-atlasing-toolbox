from pathlib import Path

from utils.config import get_resource, get_wildcards_from_config, set_defaults
from utils.wildcards import get_params, get_wildcards


module_name = 'label_harmonization'
config = set_defaults(config,module_name, warn=False)
out_dir = Path(config['output_dir']) / module_name

parameters = get_wildcards_from_config(
    config=config['DATASETS'],
    config_keys=config['defaults']['datasets'],
    config_params=['label', module_name],
    wildcard_names=['dataset', 'label_key', 'module'],
)
parameters = parameters[~parameters['module'].isnull()]


def get_input(wildcards, query=['input', module_name]):
    value = config['DATASETS'][wildcards.dataset]
    for q in query:
        value = value[q]
    return value


rule remap_labels:
    input:
        anndata=get_input,
        mapping=lambda wildcards: get_input(wildcards, query=[module_name, 'mapping', 'file'])
    output:
        h5ad=out_dir / 'remap_labels' / '{dataset}.h5ad',
    params:
        mapping_order=lambda wildcards: get_input(wildcards, query=[module_name, 'mapping', 'order']),
    conda:
        'envs/scanpy.yaml'
    resources:
        partition=lambda w: get_resource(config,resource_key='partition'),
        qos=lambda w: get_resource(config,resource_key='qos'),
        mem_mb=lambda w: get_resource(config,resource_key='mem_mb'),
    script:
        'scripts/remap_labels.py'


rule remap_labels_all:
    input:
        expand(rules.remap_labels.output, dataset=parameters['dataset'].unique())


rule all:
    input:
        rules.remap_labels_all.input
    default_target: True


module common:
    snakefile: "../common/Snakefile"
    config: config


use rule * from common as common_ *


rule dependency_graph:
    input:
        expand(
            rules.common_dependency_graph.input,
            images=config['images'] + f'/{module_name}',
            target='all'
        )