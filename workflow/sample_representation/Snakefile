"""
Sample representations
"""
from pathlib import Path
import pandas as pd

from utils.environments import get_env
from utils.ModuleConfig import ModuleConfig

mcfg = ModuleConfig(
    module_name='sample_representation',
    config=config,
    config_params=[
        'sample_key',
        'cell_type_key',
        'use_rep',
        'n_epochs',
        'norm_counts',
        'raw_counts',
    ],
)

paramspace = mcfg.get_paramspace()


module common:
    snakefile: "../common/Snakefile"
    config: config

use rule * from common as common_ *


rule pseudobulk:
    input:
        zarr=lambda wildcards: mcfg.get_input_file(**wildcards)
    output:
        zarr=directory(mcfg.out_dir / paramspace.wildcard_pattern / 'pseudobulk.zarr'),
    params:
        bulk_by=lambda wildcards: mcfg.get_from_parameters(wildcards, 'sample_key')
    conda:
        get_env(config, 'scanpy')
    resources:
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='cpu',resource_key='mem_mb', attempt=attempt)
    script:
        'scripts/pseudobulk.py'


rule composition:
    input:
        zarr=lambda wildcards: mcfg.get_input_file(**wildcards)
    output:
        zarr=directory(mcfg.out_dir / paramspace.wildcard_pattern / 'composition.zarr'),
    params:
        sample_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'sample_key'),
        cell_type_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'cell_type_key'),
        use_rep=lambda wildcards: mcfg.get_from_parameters(wildcards, 'use_rep')
    conda:
        get_env(config, 'sample_representation')
    resources:
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='cpu',resource_key='mem_mb', attempt=attempt)
    script:
        'scripts/composition.py'


rule cell_type_pseudobulk:
    input:
        zarr=lambda wildcards: mcfg.get_input_file(**wildcards)
    output:
        zarr=directory(mcfg.out_dir / paramspace.wildcard_pattern / 'cell_type_pseudobulk.zarr'),
    params:
        sample_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'sample_key'),
        cell_type_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'cell_type_key'),
        use_rep=lambda wildcards: mcfg.get_from_parameters(wildcards, 'use_rep')    
    conda:
        get_env(config, 'sample_representation')
    resources:
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='cpu',resource_key='mem_mb', attempt=attempt)
    script:
        'scripts/cell_type_pseudobulk.py'


rule pilot:
    input:
        zarr=lambda wildcards: mcfg.get_input_file(**wildcards)
    output:
        zarr=directory(mcfg.out_dir / paramspace.wildcard_pattern / 'pilot.zarr'),
    params:
        sample_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'sample_key'),
        cell_type_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'cell_type_key'),
        use_rep=lambda wildcards: mcfg.get_from_parameters(wildcards, 'use_rep')    
    conda:
        get_env(config, 'sample_representation')
    resources:
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='cpu',resource_key='mem_mb', attempt=attempt)
    script:
        'scripts/pilot.py'

rule scpoli:
    input:
        zarr=lambda wildcards: mcfg.get_input_file(**wildcards)
    output:
        zarr=directory(mcfg.out_dir / paramspace.wildcard_pattern / 'scpoli.zarr'),
    params:
        sample_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'sample_key'),
        cell_type_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'cell_type_key'),
        use_rep=lambda wildcards: mcfg.get_from_parameters(wildcards, 'raw_counts'),
        n_epochs=lambda wildcards: mcfg.get_from_parameters(wildcards, 'n_epochs'),
    conda:
        get_env(config, 'sample_representation')
    resources:
        partition=lambda w, attempt: mcfg.get_resource(profile='gpu',resource_key='partition',attempt=attempt),
        qos=lambda w, attempt: mcfg.get_resource(profile='gpu',resource_key='qos',attempt=attempt),
        gpu=lambda w, attempt: mcfg.get_resource(profile='gpu',resource_key='gpu',attempt=attempt),
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='gpu',resource_key='mem_mb',attempt=attempt),
    script:
        'scripts/scpoli.py'

rule mrvi:
    input:
        zarr=lambda wildcards: mcfg.get_input_file(**wildcards)
    output:
        zarr=directory(mcfg.out_dir / paramspace.wildcard_pattern / 'mrvi.zarr'),
    params:
        sample_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'sample_key'),
        cell_type_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'cell_type_key'),
        use_rep=lambda wildcards: mcfg.get_from_parameters(wildcards, 'raw_counts'),
        n_epochs=lambda wildcards: mcfg.get_from_parameters(wildcards, 'n_epochs'),
    conda:
        get_env(config, 'scvi-tools')
    resources:
        partition=lambda w, attempt: mcfg.get_resource(profile='gpu',resource_key='partition',attempt=attempt,attempt_to_cpu=2),
        qos=lambda w, attempt: mcfg.get_resource(profile='gpu',resource_key='qos',attempt=attempt,attempt_to_cpu=2),
        gpu=lambda w, attempt: mcfg.get_resource(profile='gpu',resource_key='gpu',attempt=attempt,attempt_to_cpu=2),
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='gpu',resource_key='mem_mb',attempt=attempt,attempt_to_cpu=2),
    script:
        'scripts/mrvi.py'


rule gloscope:
    input:
        zarr=lambda wildcards: mcfg.get_input_file(**wildcards)
    output:
        zarr=directory(mcfg.out_dir / paramspace.wildcard_pattern / 'gloscope.zarr'),
    params:
        sample_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'sample_key'),
        use_rep=lambda wildcards: mcfg.get_from_parameters(wildcards, 'use_rep'),
        k=25,
        seed=0,
    conda:
        get_env(config, 'gloscope')
    threads: 10
    resources:
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='cpu',resource_key='mem_mb', attempt=attempt)
    script:
        'scripts/gloscope.R'


rule scitd:
    input:
        zarr=lambda wildcards: mcfg.get_input_file(**wildcards)
    output:
        zarr=directory(mcfg.out_dir / paramspace.wildcard_pattern / 'scitd.zarr'),
    params:
        sample_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'sample_key'),
        cell_type_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'cell_type_key'),
        use_rep=lambda wildcards: mcfg.get_from_parameters(wildcards, 'raw_counts'),
        seed=0,
        cran_url=config.get('cran_url', 'https://cloud.r-project.org'),
    conda:
        get_env(config, 'scitd')
    threads: 10
    resources:
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='cpu',resource_key='mem_mb', attempt=attempt)
    script:
        'scripts/scitd.R'


# pca_1_2=mcfg.image_dir / paramspace.wildcard_pattern / 'pc_1_2.png',
# pca_2_3=mcfg.image_dir / paramspace.wildcard_pattern / 'pca_2_3.png',
# pca_scree=mcfg.image_dir / paramspace.wildcard_pattern / 'pca_screeplot.png',


# use rule pseudobulk as pseudobulk_organ with:
#     input:
#         zarr=rules.load_data_merge_organ.output.zarr
#     params:
#         dataset=lambda wildcards: wildcards.organ,
#         bulk_by='sample',
#         color=[
#             'dataset',
#             'study',
#             'reference',
#             'sex',
#             'disease',
#             'assay',
#             'modalities',
#             'pipeline_version',
#             'institution',
#             'self_reported_ethnicity',
#             'development_stage'
#         ],
#     output:
#         pca_1_2=images_dir / 'organ' / '{organ}_1_2.png',
#         pca_2_3=images_dir / 'organ' / '{organ}_2_3.png',
#         pca_scree=images_dir / 'organ' / '{organ}_scree.png',
#     resources:
#         mem_mb=get_resource(config,profile='cpu_merged',resource_key='mem_mb')

rule all:
    input:
        mcfg.get_output_files(rules.pseudobulk.output),
        mcfg.get_output_files(rules.composition.output),
        mcfg.get_output_files(rules.cell_type_pseudobulk.output),
        mcfg.get_output_files(rules.pilot.output),
        mcfg.get_output_files(rules.scpoli.output),
        mcfg.get_output_files(rules.mrvi.output),
        mcfg.get_output_files(rules.gloscope.output),
        mcfg.get_output_files(rules.scitd.output),
    default_target: True
    localrule: True


rule dependency_graph:
    input:
        expand(
            rules.common_dependency_graph.input,
            images=mcfg.image_dir,
            target='all'
        )
    localrule: True
