"""
Metrics
"""
from pathlib import Path
import pandas as pd
import warnings

from utils.environments import get_env
from MetricsConfig import MetricsConfig


os.environ['LD_LIBRARY_PATH'] = os.environ['CONDA_EXE'].replace('bin/conda', 'lib')
os.environ['XLA_PYTHON_CLIENT_PREALLOCATE'] = 'false'
os.environ['TF_CPP_MIN_LOG_LEVEL'] = '0'

envvars:
    'LD_LIBRARY_PATH',
    'XLA_PYTHON_CLIENT_PREALLOCATE',
    'TF_CPP_MIN_LOG_LEVEL',

mcfg = MetricsConfig(
    module_name='metrics',
    config=config,
    parameters=workflow.source_path('params.tsv'),
    config_params=[
        'label',
        'batch',
        'metrics',
        'methods', # deprecated
        'unintegrated',
        'corrected',
        'overwrite_file_id',
        'cluster_algorithm',
        'covariates',
        'gene_sets',
    ],
    wildcard_names=['label', 'batch'],
    rename_config_params={'methods': 'metric', 'metrics': 'metric'},
    explode_by=['metric', 'batch', 'label'],
    paramspace_kwargs=dict(filename_params=['label', 'batch'], filename_sep='--'),
    dtypes={
        'overwrite_file_id': bool,
    }
)
paramspace = mcfg.get_paramspace()

wildcard_constraints:
    dataset='\w+',
    method='\w+',
    batch='((?![/]).)*',
    label='((?![/]).)*',
    hyperparams='((?![/]).)*',
    lineage_key='((?![/]).)*',
    lineage_specific='((?![/]).)*',


module clustering:
    snakefile: "../../clustering/rules/rules.smk"
    config: config

module common:
    snakefile: "../common/Snakefile"
    config: config


use rule * from common as common_ *


include: 'rules/prepare.smk'
include: 'rules/metrics.smk'
include: 'rules/merge.smk'
include: 'rules/plots.smk'

rule all:
    input:
        rules.prepare_all.input,
        rules.run_all.input,
        rules.merge_metrics_all.input,
        rules.plots_all.input
    default_target: True
    localrule: True


rule dependency_graph:
    input:
        expand(
            rules.common_dependency_graph.input,
            images=mcfg.image_dir,
            target='all'
        )
    localrule: True
