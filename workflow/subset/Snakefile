"""
Data Subsetting for benchmark
"""
from pathlib import Path

from utils.environments import get_env
from utils.ModuleConfig import ModuleConfig

module_name = 'subset'
mcfg = ModuleConfig(
    module_name,
    config,
    config_params=['n_cells', 'sample', 'label', 'backed', 'dask'],
    # wildcard_names=['strategy'], # TODO manage wildcards in ModuleConfig
)
paramspace = mcfg.get_paramspace()

module common:
    snakefile: "../common/Snakefile"
    config: config

use rule * from common as common_ *

rule subset:
    input:
        lambda wildcards: mcfg.get_input_file(wildcards.dataset, wildcards.file_id),
    output:
        zarr=directory(mcfg.out_dir / paramspace.wildcard_pattern / '{strategy}.zarr')
    resources:
        mem_mb=lambda wildcards, attempt: mcfg.get_resource(profile='cpu',resource_key='mem_mb', attempt=attempt),
        disk_mb=mcfg.get_resource(profile='cpu',resource_key='disk_mb'),
    params:
        n_cells=lambda wildcards: mcfg.get_from_parameters(wildcards, 'n_cells', as_type=int, check_query_keys=False),
        sample_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'sample', check_query_keys=False),
        label_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'label', check_query_keys=False),
        backed=lambda wildcards: mcfg.get_from_parameters(wildcards, 'backed', default=True, as_type=bool, check_query_keys=False),
        dask=lambda wildcards: mcfg.get_from_parameters(wildcards, 'dask', default=True, as_type=bool, check_query_keys=False),
    conda:
        get_env(config, 'scanpy', env_dir='envs')
    script:
        'scripts/{wildcards.strategy}.py'


rule all:
    input:
        expand(
            expand(rules.subset.output, zip, **mcfg.get_input_file_wildcards(), allow_missing=True),
            strategy=['by_sample', 'within_sample']
        )
    default_target: True
    localrule: True


rule dependency_graph:
    input:
        expand(
            rules.common_dependency_graph.input,
            images=mcfg.image_dir,
            target='all'
        )
    localrule: True
