from pathlib import Path
import pandas as pd

from utils.environments import get_env
from utils.ModuleConfig import ModuleConfig


mcfg = ModuleConfig(
    module_name='relabel',
    config=config,
    # config_params=['new_columns', 'merge_columns'],
)
paramspace = mcfg.get_paramspace()


def relabel_input_files(wildcards):
    input_files = {'anndata': mcfg.get_input_file(**wildcards)}
    
    new_column_config = mcfg.get_for_dataset(wildcards.dataset, query=[mcfg.module_name, 'new_columns'])
    if isinstance(new_column_config, dict):
        assert 'file' in new_column_config, f'new_columns config must contain a file key for {wildcards}'
        assert 'order' in new_column_config, f'new_columns config must contain a order key for {wildcards}'
        input_files['new_columns'] = new_column_config.get('file')
    
    merge_column_config = mcfg.get_for_dataset(wildcards.dataset, query=[mcfg.module_name, 'merge_columns'])
    if isinstance(merge_column_config, dict):
        assert 'file' in merge_column_config, f'merge_columns config must contain a file key for {wildcards}'
        input_files['merge_columns'] = merge_column_config.get('file')

    return input_files


rule relabel:
    input:
        unpack(relabel_input_files)
    output:
        zarr=directory(mcfg.out_dir / f'{paramspace.wildcard_pattern}.zarr'),
    params:
        mapping_order=lambda wildcards: mcfg.get_for_dataset(wildcards.dataset, query=[mcfg.module_name, 'new_columns', 'order']),
        merge_sep=lambda wildcards: mcfg.get_for_dataset(wildcards.dataset, query=[mcfg.module_name, 'merge_columns', 'sep'], default='-'),
    conda:
        get_env(config, 'scanpy', env_dir='envs')
    resources:
        partition=lambda w: mcfg.get_resource(profile='cpu',resource_key='partition'),
        qos=lambda w: mcfg.get_resource(profile='cpu',resource_key='qos'),
        mem_mb=lambda w: mcfg.get_resource(profile='cpu',resource_key='mem_mb'),
    script:
        'script.py'


rule all:
    input:
        mcfg.get_output_files(rules.relabel.output)
    default_target: True
    localrule: True


module common:
    snakefile: "../common/Snakefile"
    config: config


use rule * from common as common_ *


rule dependency_graph:
    input:
        expand(
            rules.common_dependency_graph.input,
            images=mcfg.image_dir,
            target='all'
        )
    localrule: True
