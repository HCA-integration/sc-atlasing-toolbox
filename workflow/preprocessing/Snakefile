"""
Preprocessing
"""
from pathlib import Path
import pandas as pd


module_name = 'preprocessing'
dataset_df = pd.read_table(workflow.source_path(config['dataset_meta']),comment='#')
out_dir = Path(config['output_dir']) / module_name

wildcard_constraints:
    dataset='\w+',
    study='\w+',
    organ='\w+',


module common:
    snakefile: "../common/Snakefile"
    config: config

use rule * from common as common_ *


module load_data:
    snakefile: "../load_data/Snakefile"
    config: config

use rule * from load_data as load_data_ *


rule normalize:
    input:
        zarr=rules.load_data_merge_organ.output.zarr
    output:
        zarr=directory(out_dir / '{organ}' / 'normalized.zarr')
    conda:
        'envs/scanpy.yaml'
    script:
        'scripts/normalize.py'


rule highly_variable_genes:
    input:
        zarr=rules.normalize.output.zarr
    output:
        zarr=directory(out_dir / '{organ}' / 'highly_variable_genes.zarr')
    params:
        n_hvgs=2000,
        batch='donor',
    conda:
        'envs/scanpy.yaml'
    script:
        'scripts/highly_variable_genes.py'


rule pca:
    input:
        zarr=rules.highly_variable_genes.output.zarr
    output:
        zarr=directory(out_dir / '{organ}' / 'pca.zarr')
    conda:
        'envs/scanpy.yaml'
    script:
        'scripts/pca.py'


rule neighbors:
    input:
        zarr=rules.pca.output.zarr
    output:
        zarr=directory(out_dir / '{organ}' / 'neighbors.zarr')
    conda:
        'envs/scanpy.yaml'
    script:
        'scripts/neighbors.py'


rule umap:
    input:
        zarr=rules.neighbors.output.zarr
    output:
        zarr=directory(out_dir / '{organ}' / 'umap.zarr')
    conda:
        'envs/scanpy.yaml'
    script:
        'scripts/umap.py'


rule all:
    input:
        expand(rules.neighbors.output,organ='blood'),
        expand(rules.umap.output,organ='blood')
    default_target: True
